plugins {
  id 'org.springframework.boot' version '2.1.7.RELEASE' apply true
  id "io.spring.dependency-management" version "1.0.8.RELEASE"
  id 'java'
  id 'idea'
  id 'com.gorylenko.gradle-git-properties' version '2.1.0'
  id 'com.avast.gradle.docker-compose' version '0.9.4'
  id 'com.google.cloud.tools.jib' version '1.5.1'
  id 'org.unbroken-dome.helm' version '0.4.0'
  id 'org.unbroken-dome.helm-publish' version '0.4.0'
  id 'org.unbroken-dome.helm-releases' version '0.4.0'

  id 'se.patrikerdes.use-latest-versions' version '0.2.12'
  id 'com.github.ben-manes.versions' version '0.24.0'

   id 'maven-publish'
}

ext {
  set('activeSpringProfiles', 'local')
  set('tag', 'local')
  set('chartRepoUrl', 'http://chartmuseum.default.svc.cluster.local/')
}

group = 'com.gregturnquist.learningspringboot'

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
  implementation enforcedPlatform('org.springframework.cloud:spring-cloud-dependencies:Greenwich.SR2')
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.cloud:spring-cloud-config-server'
}

sourceCompatibility = 11
targetCompatibility = 11

bootJar {
  baseName = 'lsb2-config-server'
  excludeDevtools = false
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.gregturnquist.learningspringboot'
            from components.java
        }
    }
}

helm {
  // basic helm plugin config
  home = file("${project.buildDir}/helm/home")
  outputDir = file("${project.buildDir}/helm")
// The global lint configuration applies to all charts
  lint {
      // treat linter warnings as errors (failing the build)
      strict = false
      enabled = true
  }
  filtering {
    // This value will be resolvable in all charts' YAML files as ${authorName}
    values.put 'authorName', 'Developer'
    values.put 'image.tag', project.version.toString()
  }
  repositories {
      museum {
          url = uri(chartRepoUrl)
      }
  }
  // helm-release plugin config
  releases {
      'lsb2-config-server' {
          from charts.main
          namespace = 'local'
          replace = false
          purge = true
          dryRun = false
          wait = false
          // pass values (like --set on the command line)
          values = [ 'image.tag': project.version ]
          // pass value files (like -f on the command line)
          valueFiles.from "src/main/helm/values.yaml"
          valueFiles.from "src/main/helm/values-${activeSpringProfiles}.yaml"
      }
  }
  // helm-publish plugin config
  publishing {
      repositories {
          museum {
              url = uri(chartRepoUrl)
          }
      }
  }  
}

/*
 * docker-compose plugin configuration.
 * Details on https://github.com/avast/gradle-docker-compose-plugin
 */
dockerCompose {
  useComposeFiles = ['docker/env/local.yml'] // like 'docker-compose -f <file>'; default is empty
  buildBeforeUp = true // performs 'docker-compose build' before calling the 'up' command; default is true

  waitForTcpPorts = true // turns off the waiting for exposed TCP ports opening
  captureContainersOutput = true // if true, prints output of all containers to Gradle output - very useful for debugging; default is false

  stopContainers = true // doesn't call `docker-compose down` - see below the paragraph about reconnecting
  removeContainers = true
  removeOrphans = false // removes containers for services not defined in the Compose file

  environment.put 'TAG', version
}
composeBuild.dependsOn bootJar
composeUp.dependsOn composeBuild
composePush.dependsOn composeBuild

jib {
  from {
    image = 'azul/zulu-openjdk-alpine:11.0.3'
  }
  to {
    image = 'jghamburg/lsb2-config-server:local'
    // use values from $HOME/.gradle/gradle.properties
//    auth{
//      username = dockerRegistryUsername
//      password = dockerRegistryPassword
//    }
    credHelper = 'osxkeychain'
  }
  container {
    jvmFlags = ['-Djava.security.egd=file:/dev/./urandom', '-Xms512m', '-Xdebug', "-Dspring.profiles.active=${activeSpringProfiles}".toString()]
    mainClass = 'com.greglturnquist.learningspringboot.LearningSpringBootConfigServer'
    args = []
    ports = ['8888']
    format = 'OCI'
    useCurrentTimestamp = true
  }
}


task semanticRelease (type: Exec){
  workingDir '.'
  commandLine 'semantic-release', '-b', 'master'
}

